(if (= @app-name "XADRARAutoInstall") (set #AutoInstall 1))

(set @default-dest "libs:xad")

(if (= #AutoInstall 1)
	(
		(set #rar (getversion "libs:xad/RAR"))

		(if (= (/ (getversion "libs:xad/RAR") 65536) 1)
			(
				(message "XAD RAR AutoInstall - PLEASE READ\n\n"
				"You have selected to update the XAD RAR client, "
				"however the version on your system is the original "
        		"one which ships with OS4.  If you proceed it will "
				"be updated with a version written by a different "
                "party, which may not support all the features of "
                "the original.\n\n"
				"This client DOES NOT support: MS self-extracting RARs.\n"
				"However, it DOES support: RAR3 archives.\n\n"
				"If you decide to proceed, you can revert back to the "
				"original at any time using AmiUpdate's System_Rollback "
				"application."  (all))
			)
		)

		(run "CopyStore OS4/RAR libs:xad")
		(exit (quiet))
	)
	;else
	(
		(if (>= (/ (getversion) 65536) 50)
			(
				(set #os-ver 0)
				(set #os4 "AmigaOS 4.0 or later (PowerPC)")
			)
		;else
			(
				(set #os-ver 1)
				(set #os4 "")
			)
		)

		(welcome)

		(set #os-ver
			(askchoice
				(prompt "Please select the version of the OS you are using")
				(help @askchoice-help)
				(choices #os4
						"Versions prior to AmigaOS 4.0 (680x0)")
				(default #os-ver)
			)
		)

		(set #src-dir "OS4")
		(if (= #os-ver 1) (set #src-dir "OS3"))

		(copylib
			(prompt "Check RAR XAD client is newer than any existing version!")
			(help @copylib-help)
			(source (tackon #src-dir "RAR"))
			(dest @default-dest)
			(confirm "expert")
		)

		(exit "You must reboot for the client to take effect." (quiet))
	)
)
